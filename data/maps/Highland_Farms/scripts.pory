mapscripts Highland_Farms_MapScripts{
    MAP_SCRIPT_ON_LOAD: HF_UpdateMetatiles
    MAP_SCRIPT_ON_TRANSITION: HF_UpdateSpritePos
    MAP_SCRIPT_ON_RESUME: HF_RemoveRivalPokemon
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_ADVENTURE_START, 24: HF_Mike_Farewell
    ]
}

script TestBattle_HF{
    trainerbattle_no_intro(TRAINER_CALVIN_1, HF_T_MikeLoseBattle)
    end
}

script HF_RemoveRivalPokemon{
    specialvar(VAR_RESULT, GetBattleOutcome)
    if(var(VAR_RESULT) == B_OUTCOME_WON){
        removeobject(11)
        removeobject(12)
        removeobject(13)
    }
    if(var(VAR_RESULT) == B_OUTCOME_LOST){
        removeobject(8)
        removeobject(9)
        removeobject(10)
    }
    else{
    }
    end
}

script HF_RivalShed_IsDoorLocked{
    if(flag(FLAG_UNLOCK_RIVAL_SHED) == TRUE){
        setflag(FLAG_SUPPRESS_SPEAKER_NAME)
        msgbox("The door is unlocked.")
        waitmessage
        closemessage
        clearflag(FLAG_SUPPRESS_SPEAKER_NAME)
    }
    else{
        setflag(FLAG_SUPPRESS_SPEAKER_NAME)
        msgbox("The door is locked.")
        waitmessage
        closemessage
        clearflag(FLAG_SUPPRESS_SPEAKER_NAME)
    }
    release
    end
}

script HF_UpdateMetatiles{
    call(CheckSproutFlag)
    call(HF_CheckLogFlag)
    end
}

script HF_UpdateSpritePos{
    if(var(VAR_ADVENTURE_START) == 18){
        setobjectxyperm(1, 12, 19)
        setobjectxyperm(4, 11, 19)
        setobjectmovementtype(1, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
        setobjectmovementtype(4, MOVEMENT_TYPE_FACE_RIGHT)
    }
    elif(var(VAR_ADVENTURE_START) == 19){
        setobjectxyperm(4, 10, 19)
        setobjectmovementtype(4, MOVEMENT_TYPE_FACE_DOWN)
    }
    elif(var(VAR_ADVENTURE_START) == 24){
        setobjectxyperm(3, 41, 35)
        setobjectmovementtype(3, MOVEMENT_TYPE_FACE_DOWN)
    }
    else{
    }
    end
}

script HF_PlayerHouse_Sign{
    setflag(FLAG_SUPPRESS_SPEAKER_NAME)
    msgbox("{PLAYER}'s House", MSGBOX_SIGN)
    closemessage
    clearflag(FLAG_SUPPRESS_SPEAKER_NAME)
    release
    end
}

script HF_RivalHouse_Sign{
    setflag(FLAG_SUPPRESS_SPEAKER_NAME)
    msgbox("Mike's House", MSGBOX_SIGN)
    closemessage
    clearflag(FLAG_SUPPRESS_SPEAKER_NAME)
    release
    end
}

script HF_Mike_Interact{
    if(var(VAR_ADVENTURE_START) == 20){
        faceplayer
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
        msgbox(HF_T_Mike8)
        waitmessage
        closemessage
        turnobject(4, DIR_WEST)
    }
    elif(var(VAR_ADVENTURE_START) == 18){
        faceplayer
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
        msgbox(HF_T_Mike3)
        waitmessage
        closemessage
        turnobject(4, DIR_EAST)
    }
    else{
    }
    release
    end
}

script HF_Mamaw_Interact{
    faceplayer
    speakername("{COLOR RED}{SHADOW LIGHT_RED}{FONT_SHORT}Mamaw")
    msgbox(HF_T_Mamaw1)
    waitmessage
    closemessage
    turnobject(5, DIR_WEST)
    release
    end
}

script HF_Millie_Interact{
    if(var(VAR_ADVENTURE_START) == 18){
        lock
        faceplayer
        playmoncry(SPECIES_MILTANK, 0)
        waitmoncry
        playse(SE_PIN)
        applymovement(1, Univ_Heart)
        waitmovement(0)
        setflag(FLAG_SUPPRESS_SPEAKER_NAME)
        msgbox("Millie loves to be brushed!")
        waitmessage
        closemessage
        clearflag(FLAG_SUPPRESS_SPEAKER_NAME)
    }
    else{
        lock
        faceplayer
        playmoncry(SPECIES_MILTANK, 0)
        waitmoncry
    }
    release
    end
}

script HF_DadInteract{
    if(var(VAR_ADVENTURE_START) == 22){
        fadeoutbgm(2)
        playbgm(MUS_END, FALSE)
        delay(180)
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
        msgbox(HF_T_Dad3, MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            closemessage
            faceplayer
            speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
            msgbox(HF_T_Dad4_Yes)
            waitmessage
            closemessage
            turnobject(16, DIR_WEST)
        }
        else{
            closemessage
            faceplayer
            speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
            msgbox(HF_T_Dad4_No)
            waitmessage
            closemessage
            turnobject(16, DIR_WEST)
        }
    setvar(VAR_ADVENTURE_START, 23)
    fadeoutbgm(2)
    playbgm(MUS_FALLARBOR, FALSE)
    }
    else{
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
        msgbox("...")
        waitmessage
        closemessage
    }
    release
    end
}


script HF_DadIntro_A{
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_1A)
    waitmovement(0)
    applymovement(2, HF_M_Dad1)
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    waitmovement(2)
    setmetatile(47, 21, 0x00EF, TRUE)
    playse(SE_BALL_BOUNCE_1)
    special(DrawWholeMapView)
    waitse
    delay(30)
    applymovement(2, HF_M_Dad2)
    waitmovement(0)
    applymovement(2, HF_M_Dad1)
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    waitmovement(2)
    setmetatile(46, 21, 0x0268, TRUE)
    playse(SE_BALL_BOUNCE_1)
    special(DrawWholeMapView)
    waitse
    delay(30)
    applymovement(2, HF_M_Dad2)
    waitmovement(0)
    applymovement(2, HF_M_Dad1)
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    waitmovement(2)
    setmetatile(45, 21, 0x026A, TRUE)
    playse(SE_BALL_BOUNCE_1)
    special(DrawWholeMapView)
    waitse
    delay(30)
    applymovement(2, HF_M_Dad3)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_M_Dad3)
    waitmovement(0)
    delay(60)
    turnobject(2, DIR_SOUTH)
    applymovement(2, Univ_Exclam)
    playse(SE_PIN)
    waitmovement(0)
    waitse
    delay(60)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
    msgbox(HF_T_Dad1)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, HF_M_Player1A)
    waitmovement(0)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
    msgbox(HF_T_Dad2)
    closemessage
    applymovement(2, HF_M_Dad4)
    waitmovement(0)
    opendoor(45, 15)
    waitdooranim
    applymovement(2, Univ_StepUp1)
    waitmovement(0)
    playse(SE_EXIT)
    removeobject(2)
    waitse
    closedoor(45, 15)
    waitdooranim
    applymovement(OBJ_EVENT_ID_CAMERA, Univ_StepDown1)
    waitmovement(0)
    special(RemoveCameraObject)
    setflag(FLAG_HIDE_DAD_1)
    setflag(FLAG_SPROUTS_PLANTED)
    setvar(VAR_ADVENTURE_START, 4)
    release
    end
}

script HF_DadIntro_B{
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_1B)
    waitmovement(0)
    applymovement(2, HF_M_Dad1)
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    waitmovement(2)
    setmetatile(47, 21, 0x00EF, TRUE)
    playse(SE_BALL_BOUNCE_1)
    special(DrawWholeMapView)
    waitse
    delay(30)
    applymovement(2, HF_M_Dad2)
    waitmovement(0)
    applymovement(2, HF_M_Dad1)
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    waitmovement(2)
    setmetatile(46, 21, 0x0268, TRUE)
    playse(SE_BALL_BOUNCE_1)
    special(DrawWholeMapView)
    waitse
    delay(30)
    applymovement(2, HF_M_Dad2)
    waitmovement(0)
    applymovement(2, HF_M_Dad1)
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    playse(SE_M_DIG)
    waitse
    waitmovement(2)
    setmetatile(45, 21, 0x026A, TRUE)
    playse(SE_BALL_BOUNCE_1)
    special(DrawWholeMapView)
    waitse
    delay(30)
    applymovement(2, HF_M_Dad3)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_M_Dad3)
    waitmovement(0)
    delay(60)
    turnobject(2, DIR_SOUTH)
    applymovement(2, Univ_Exclam)
    playse(SE_PIN)
    waitmovement(0)
    waitse
    delay(60)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
    msgbox(HF_T_Dad1)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, HF_M_Player1B)
    waitmovement(0)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Dad")
    msgbox(HF_T_Dad2)
    closemessage
    applymovement(2, HF_M_Dad4)
    waitmovement(0)
    opendoor(45, 15)
    waitdooranim
    applymovement(2, Univ_StepUp1)
    waitmovement(0)
    playse(SE_EXIT)
    removeobject(2)
    waitse
    closedoor(45, 15)
    waitdooranim
    applymovement(OBJ_EVENT_ID_CAMERA, Univ_StepDown1)
    waitmovement(0)
    special(RemoveCameraObject)
    setflag(FLAG_HIDE_DAD_1)
    setflag(FLAG_SPROUTS_PLANTED)
    setvar(VAR_ADVENTURE_START, 4)
    release
    end
}

script CheckSproutFlag{
    if(flag(FLAG_SPROUTS_PLANTED) == TRUE){
        setmetatile(47, 21, 0x00EF, TRUE)
        setmetatile(46, 21, 0x0268, TRUE)
        setmetatile(45, 21, 0x026A, TRUE)
    }
    else{
    }
    return
}

script HF_CheckLogFlag{
    if(flag(FLAG_HF_REMOVE_LOG) == TRUE){
        setmetatile(2, 12, 0x000, FALSE)
        setmetatile(3, 12, 0x000, FALSE)
    }
    else{
    }
    return
}

script BattleMike{
    lock
    faceplayer
    trainerbattle_no_intro(TRAINER_RIVAL_MIKE, LoseText)
    release
    end
}

script HF_MikeIntro{
    turnobject(3, DIR_NORTH)
    applymovement(3, Univ_Exclam)
    playse(SE_PIN)
    waitmovement(0)
    waitse
    delay(60)
    applymovement(3, HF_W_Mike1)
    waitmovement(0)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike1)
    waitmessage
    closemessage
    applymovement(3, HF_W_Mike2)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    waitmovement(3)
    opendoor(41, 33)
    waitdooranim
    applymovement(3, Univ_StepUp1)
    waitmovement(0)
    playse(SE_EXIT)
    removeobject(3)
    waitse
    closedoor(41, 33)
    waitdooranim
    setflag(FLAG_HIDE_RIVAL_MIKE_1)
    setvar(VAR_ADVENTURE_START, 8)
    clearflag(FLAG_HIDE_RIVAL_MIKE_2)
    release
    end
}

script HF_Mike_ReadyToGo{
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_2)
    waitmovement(0)
    setmetatile(10, 16, 0x219, TRUE)
    special(DrawWholeMapView)
    delay(20)
    playse(SE_DOOR)
    opendoor(10, 16)
    waitdooranim
    addobject(4)
    applymovement(4, Univ_StepDown1)
    waitmovement(0)
    closedoor(10, 16)
    waitdooranim
    applymovement(4, Univ_Exclam)
    playse(SE_PIN)
    waitmovement(0)
    waitse
    delay(60)
    applymovement(4, HF_W_Mike3)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_3)
    waitmovement(4)
    special(RemoveCameraObject)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike2, MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, HF_W_Player2)
    waitmovement(0)
    applymovement(4, HF_W_Mike4)
    waitmovement(0)
    removeobject(4)
    setmetatile(10, 16, 0x226, TRUE)
    special(DrawWholeMapView)
    setvar(VAR_ADVENTURE_START, 14)
    clearflag(FLAG_OBR_HIDE_RIVAL_MIKE)
    release
    end
}

script HF_FirstBattleIntro_Trigger{
    setmetatile(2, 12, 0x000, FALSE)
    setmetatile(3, 12, 0x000, FALSE)
    setflag(FLAG_HF_REMOVE_LOG)
    turnobject(4, DIR_SOUTH)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_2)
    waitmovement(0)
    delay(20)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike4)
    waitmessage
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, HF_M_Player2)
    waitmovement(0)
    delay(20)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike5, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
        msgbox(HF_T_Mike6_Yes)
        waitmessage
        closemessage
    }
    else{
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
        msgbox(HF_T_Mike6_No)
        waitmessage
        closemessage
    }
    applymovement(4, HF_M_Mike5)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_M_Mike5)
    applymovement(OBJ_EVENT_ID_PLAYER, HF_M_Player3)
    waitmovement(0)
    delay(10)
    applymovement(OBJ_EVENT_ID_PLAYER, Univ_Exclam)
    playse(SE_PIN)
    waitmovement(0)
    waitse
    delay(100)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike7)
    waitmessage
    closemessage
    applymovement(4, HF_M_Mike6)
    waitmovement(0)
    setobjectxyperm(4, 11, 5)
    setobjectmovementtype(4, MOVEMENT_TYPE_FACE_LEFT)
    delay(20)
    applymovement(OBJ_EVENT_ID_CAMERA, Univ_StepLeft1)
    waitmovement(0)
    special(RemoveCameraObject)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    setvar(VAR_ADVENTURE_START, 20)
    release
    end
}

script HF_NoExitBattleStage_Trigger{
    special(SpawnCameraObject)
    applymovement(4, Univ_Exclam)
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_4)
    waitmovement(0)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike9)
    waitmessage
    closemessage
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_4R)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovement(OBJ_EVENT_ID_PLAYER, Univ_StepUp1)
    waitmovement(0)
    release
    end
} 

script HF_FirstBattleStart_Trigger{
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    special(SpawnCameraObject)
    fadeoutbgm(1)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_5)
    waitmovement(0)
    delay(30)
    playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike10)
    waitmessage
    closemessage
    delay(30)
    call(HF_HandleBattleSprites_Rival)
    delay(30)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_5R)
    waitmovement(0)
    special(RemoveCameraObject)
    delay(30)
    call(HF_HandleBattleSprites_Player)
    delay(30)
    call(HF_StartBattle_1)
    fadedefaultbgm
    delay(60)
    call(HF_RemovePlayerPokemon_PostBattle)
    delay(60)
    applymovement(4, HF_M_Mike7)
    applymovement(OBJ_EVENT_ID_PLAYER, HF_M_Player4)
    waitmovement(0)
    delay(30)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike11)
    waitmessage
    closemessage
    applymovement(4, HF_M_Mike8)
    waitmovement(0)
    removeobject(4)
    setflag(FLAG_HF_HIDE_RIVAL_MIKE)
    clearflag(FLAG_RHB1F_HIDE_RIVAL)
    setvar(VAR_ADVENTURE_START, 21)
    release
    end
}

script HF_RemovePlayerPokemon_PostBattle{
     if(var(VAR_STARTER_MON) == 2){ //Player = OSHAWOTT, Rival = TURTWIG
        applymovement(10, Univ_StepLeft1)
        waitmovement(0)
        playmoncry(SPECIES_OSHAWOTT, 0)
        waitmoncry
        playse(SE_BIKE_HOP)
        applymovement(10, Univ_JumpIP_1Left)
        waitmovement(0)
        delay(30)
        playse(SE_BALL_BOUNCE_1)
        addobject(7)
        setobjectsubpriority(7, MAP_HIGHLAND_FARMS, 0)
        setobjectsubpriority(10, MAP_HIGHLAND_FARMS, 3)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        removeobject(10)
        waitse
        delay(30)
        playse(SE_RG_BAG_POCKET)
        removeobject(7)
        waitse
    }
    elif(var(VAR_STARTER_MON) == 1){ //Player = LITTEN, Rival = OSHAWOTT
        applymovement(9, Univ_StepLeft1)
        waitmovement(0)
        playmoncry(SPECIES_LITTEN, 0)
        waitmoncry
        playse(SE_BIKE_HOP)
        applymovement(9, Univ_JumpIP_1Left)
        waitmovement(0)
        delay(30)
        playse(SE_BALL_BOUNCE_1)
        addobject(7)
        setobjectsubpriority(7, MAP_HIGHLAND_FARMS, 0)
        setobjectsubpriority(9, MAP_HIGHLAND_FARMS, 3)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        removeobject(9)
        waitse
        delay(30)
        playse(SE_RG_BAG_POCKET)
        removeobject(7)
        waitse
    }
    elif(var(VAR_STARTER_MON) == 0){ //Player = TURTWIG, Rival = LITTEN
        applymovement(8, Univ_StepLeft1)
        waitmovement(0)
        playmoncry(SPECIES_TURTWIG, 0)
        waitmoncry
        playse(SE_BIKE_HOP)
        applymovement(8, Univ_JumpIP_1Left)
        waitmovement(0)
        delay(30)
        playse(SE_BALL_BOUNCE_1)
        addobject(7)
        setobjectsubpriority(7, MAP_HIGHLAND_FARMS, 0)
        setobjectsubpriority(8, MAP_HIGHLAND_FARMS, 3)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        removeobject(8)
        waitse
        delay(30)
        playse(SE_RG_BAG_POCKET)
        removeobject(7)
        waitse
    }
    return
}

script HF_StartBattle_1{
    if(var(VAR_STARTER_MON) == 2){ //Player = OSHAWOTT, Rival = TURTWIG
        playse(SE_M_GUST2)
        applymovement(10, HF_M_PlayerPokemon)
        delay(10)
        playse(SE_M_GUST2)
        applymovement(11, HF_M_RivalPokemon)
        waitmovement(0)
        trainerbattle_no_intro(TRAINER_HF_MIKE_TURTWIG, HF_T_MikeLoseBattle)
        return
    }
    elif(var(VAR_STARTER_MON) == 1){ //Player = LITTEN, Rival = OSHAWOTT
        playse(SE_M_GUST2)
        applymovement(9, HF_M_PlayerPokemon)
        delay(10)
        playse(SE_M_GUST2)
        applymovement(13, HF_M_RivalPokemon)
        waitmovement(0)
        trainerbattle_no_intro(TRAINER_HF_MIKE_OSHAWOTT, HF_T_MikeLoseBattle)
        return
    }
    elif(var(VAR_STARTER_MON) == 0){ //Player = TURTWIG, Rival = LITTEN
        playse(SE_M_GUST2)
        applymovement(8, HF_M_PlayerPokemon)
        delay(10)
        playse(SE_M_GUST2)
        applymovement(12, HF_M_RivalPokemon)
        waitmovement(0)
        trainerbattle_no_intro(TRAINER_HF_MIKE_LITTEN, HF_T_MikeLoseBattle)
        return
    }
}

// 0=TURTWIG, 1=LITTEN, 2=OSHAWOTT

script HF_HandleBattleSprites_Rival{
    if(var(VAR_STARTER_MON) == 2){ //Rival has TURTWIG
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
        msgbox(HF_T_MikeGoTurtwig)
        closemessage
        playse(SE_BALL_THROW)
        waitse
        playse(SE_BALL_BOUNCE_1)
        addobject(6)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        addobject(11)
        removeobject(6)
        waitse
        playmoncry(SPECIES_TURTWIG, 2)
        waitmoncry
    }
    elif(var(VAR_STARTER_MON) == 1){ //Rival has OSHAWOTT
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
        msgbox(HF_T_MikeGoOshawott)
        closemessage
        playse(SE_BALL_THROW)
        waitse
        playse(SE_BALL_BOUNCE_1)
        addobject(6)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        addobject(13)
        removeobject(6)
        waitse
        playmoncry(SPECIES_OSHAWOTT, 2)
        waitmoncry
    }
    elif(var(VAR_STARTER_MON) == 0){ //Rival has LITTEN
        speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
        msgbox(HF_T_MikeGoLitten)
        closemessage
        playse(SE_BALL_THROW)
        waitse
        playse(SE_BALL_BOUNCE_1)
        addobject(6)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        addobject(12)
        removeobject(6)
        waitse
        playmoncry(SPECIES_LITTEN, 2)
        waitmoncry
    }
    return
}

script HF_HandleBattleSprites_Player{
    if(var(VAR_STARTER_MON) == 2){ //Player has OSHAWOTT
        playse(SE_BALL_THROW)
        waitse
        playse(SE_BALL_BOUNCE_1)
        addobject(7)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        addobject(10)
        removeobject(7)
        waitse
        playmoncry(SPECIES_OSHAWOTT, 2)
        waitmoncry
    }
    elif(var(VAR_STARTER_MON) == 1){ //Player has LITTEN
        playse(SE_BALL_THROW)
        waitse
        playse(SE_BALL_BOUNCE_1)
        addobject(7)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        addobject(9)
        removeobject(7)
        waitse
        playmoncry(SPECIES_LITTEN, 2)
        waitmoncry
    }
    elif(var(VAR_STARTER_MON) == 0){ //Player has TURTWIG
        playse(SE_BALL_THROW)
        waitse
        playse(SE_BALL_BOUNCE_1)
        addobject(7)
        waitse
        delay(30)
        playse(SE_BALL_OPEN)
        addobject(8)
        removeobject(7)
        waitse
        playmoncry(SPECIES_TURTWIG, 2)
        waitmoncry
    }
    return
}

script HF_DadRunFromHouse_A{
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_7A)
    waitmovement(0)
    playse(SE_DOOR)
    opendoor(41, 33)
    waitdooranim
    addobject(14)
    applymovement(14, HF_M_Dad5)
    waitmovement(0)
    closedoor(41, 33)
    waitdooranim
    applymovement(14, HF_M_Dad6)
    waitmovement(0)
    removeobject(14)
    playse(SE_DOOR)
    opendoor(41, 33)
    waitdooranim
    addobject(15)
    applymovement(15, HF_M_Mom1)
    waitmovement(0)
    closedoor(41, 33)
    speakername("{COLOR RED}{SHADOW LIGHT_RED}{FONT_SHORT}Mom")
    msgbox(HF_T_Mom1)
    waitmessage
    closemessage
    delay(30)
    turnobject(15, DIR_NORTH)
    playse(SE_DOOR)
    opendoor(41, 33)
    waitdooranim
    applymovement(15, Univ_StepUp1)
    delay(20)
    playse(SE_EXIT)
    removeobject(15)
    waitse
    closedoor(41, 33)
    delay(30)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_7A_R)
    waitmovement(0)
    special(RemoveCameraObject)
    clearflag(FLAG_HF_HIDE_DAD_2)
    setvar(VAR_ADVENTURE_START, 22)
    release
    end
}

script HF_DadRunFromHouse_B{
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_7B)
    waitmovement(0)
    playse(SE_DOOR)
    opendoor(41, 33)
    waitdooranim
    addobject(14)
    applymovement(14, HF_M_Dad5)
    waitmovement(0)
    closedoor(41, 33)
    waitdooranim
    applymovement(14, HF_M_Dad6)
    waitmovement(0)
    removeobject(14)
    playse(SE_DOOR)
    opendoor(41, 33)
    waitdooranim
    addobject(15)
    applymovement(15, HF_M_Mom1)
    waitmovement(0)
    closedoor(41, 33)
    speakername("{COLOR RED}{SHADOW LIGHT_RED}{FONT_SHORT}Mom")
    msgbox(HF_T_Mom1)
    waitmessage
    closemessage
    delay(30)
    turnobject(15, DIR_NORTH)
    playse(SE_DOOR)
    opendoor(41, 33)
    waitdooranim
    applymovement(15, Univ_StepUp1)
    delay(20)
    playse(SE_EXIT)
    removeobject(15)
    waitse
    closedoor(41, 33)
    delay(30)
    applymovement(OBJ_EVENT_ID_CAMERA, HF_CAM_7B_R)
    waitmovement(0)
    special(RemoveCameraObject)
    clearflag(FLAG_HF_HIDE_DAD_2)
    setvar(VAR_ADVENTURE_START, 22)
    release
    end
}

script HF_Mike_Farewell{
    turnobject(3, DIR_NORTH)
    playse(SE_PIN)
    applymovement(3, Univ_Exclam)
    waitmovement(0)
    delay(30)
    speakername("{COLOR BLUE}{SHADOW LIGHT_BLUE}{FONT_SHORT}Mike")
    msgbox(HF_T_Mike12)
    waitmessage
    closemessage
    applymovement(3, HF_M_Mike9)
    waitmovement(0)
    removeobject(3)
    setflag(FLAG_HIDE_RIVAL_MIKE_1)
    setvar(VAR_ADVENTURE_START, 25)
    release
    end
}

movement HF_CAM_1A{
    walk_diag_northeast * 3
    walk_up
}

movement HF_CAM_1B{
    walk_diag_northeast * 4
}

movement HF_CAM_2{
    walk_up * 3
}

movement HF_CAM_3{
    walk_fast_down * 3
}

movement HF_CAM_4{
    walk_diag_northeast * 4
}

movement HF_CAM_4R{
    walk_diag_southwest * 4
}

movement HF_CAM_5{
    walk_right * 5
}

movement HF_CAM_5R{
    walk_left * 5
}

movement HF_CAM_6{
    walk_fast_right * 2
}

movement HF_CAM_7A{
    walk_diag_northeast * 6
}

movement HF_CAM_7A_R{
    walk_diag_southwest * 6
}

movement HF_CAM_7B{
    walk_diag_northeast * 5
    walk_up * 2
}

movement HF_CAM_7B_R{
    walk_diag_southwest * 5
    walk_down * 2
}

movement HF_M_Dad1{
    walk_in_place_up * 3
}

movement HF_M_Dad2{
    walk_left
    face_up
}

movement HF_M_Dad3{
    walk_left
    walk_up
    walk_left * 2
    walk_up
}

movement HF_M_Dad4{
    walk_up * 3
    walk_right * 3
    walk_up
}

movement HF_M_Dad5{
    walk_fast_down
}

movement HF_M_Dad6{
    walk_fast_left * 4
    walk_fast_up * 5
}

movement HF_M_Player1A{
    walk_up * 5
}

movement HF_M_Player1B{
    walk_fast_right 
    walk_up * 5
}

movement HF_W_Player2{
    walk_right
    face_left
}

movement HF_M_Player4{
    walk_right * 2
}

movement HF_W_Mike1{
    walk_fast_up * 2
}

movement HF_W_Mike2{
    walk_fast_right * 4
    face_up
}

movement HF_M_Player2{
    walk_up * 2
}

movement HF_M_Player3{
    walk_up
    walk_left * 5
    walk_up * 2
    walk_left * 2
    walk_up * 6
    walk_right * 2
    walk_up * 4
    walk_right * 3
    face_up
}

movement HF_W_Mike3{
    walk_fast_down * 3
}

movement HF_W_Mike4{
    walk_faster_down * 8
}

movement HF_M_Mike5{
    walk_left * 5
    walk_up * 2
    walk_left * 2
    walk_up * 6
    walk_right * 2
    walk_up * 4
    walk_right * 4
    face_left
}

movement HF_M_Mike6{
    walk_fast_up * 2
    walk_fast_right * 2
    face_left
}

movement HF_M_Mike7{
    walk_left * 2
}

movement HF_M_Mike8{
    walk_down * 2
    walk_left * 5
    walk_down * 5
}

movement HF_M_Mike9{
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_down * 6
}

movement HF_M_Mom1{
    walk_down
    face_left
}

movement HF_M_PlayerPokemon{
    slide_right
    face_right
}

movement HF_M_RivalPokemon{
    slide_left
    face_left
}





text HF_T_Dad1{
    "Mornin', {PLAYER}!\n"
    "Come on over here, we got a job to do."
}

text HF_T_Dad2{
    "I already got them new plants in the \n"
    "ground, n' the harvest herbs moved\l"
    "over into the shed.\p"
    "If ya wanna meet me in there, we can\n"
    "get to processin' this new batch.\p"
    "I'll bet'cha we can get finished before\n"
    "Momma finishes lunch if we stick to it!\p"
    "It sure is great having yer help out\n"
    "here, {PLAYER}."
}

text HF_T_Dad3{
    "...{PAUSE 60}\n"
    "Is this really what ya want, {PLAYER}?\p"
}

text HF_T_Dad4_Yes{
    "Well, at least I didn't raise ya to lie...\p"
    "I just can't believe this life ain't good\n"
    "'nough for ya, {PLAYER}.\p"
    "I just thought you were better than\n"
    "that.\p"
    "You go on now, {PLAYER}, I'm sure you've\n"
    "got plenty of important big city things\l"
    "to be doin'.\p"
    "Just leave me be."
}

text HF_T_Dad4_No{
    "Now I know I didn't raise ya to lie...\p"
    "I just can't believe this life ain't good\n"
    "'nough for ya, {PLAYER}.\p"
    "I just thought you were better than\n"
    "that.\p"
    "You go on now, {PLAYER}, I'm sure you've\n"
    "got plenty of important big city things\l"
    "to be doin'.\p"
    "Just leave me be."
}

text HF_T_Mike1{
    "{PLAYER}!!\p"
    "We gotta watch this new Battle Tape\n"
    "that Ol' Bo lent me!\p"
    "Come on, let's go!"
}

text HF_T_Mike2{
    "Hey, that was pretty quick, {PLAYER}!\n"
    "How about a race down to Bo's ranch?!\p"
    "Three... {PAUSE 30}Two... {PAUSE 30}GO!"
}

text HF_T_Mike3{
    "Hey, {PLAYER}, almost done over here.\n"
    "Come back in a lil' bit, I got somethin'\l"
    "to show you!"
}

text HF_T_Mike4{
    "Hurry up, {PLAYER}!"
}

text HF_T_Mike5{
    "Alright, so I've been workin' on this for\n"
    "a while now...\p"
    "I knew one day we would need one of\n"
    "these, I just didn't think it would be\l"
    "this soon!\p"
    "{PLAYER}... {PAUSE 30}are you ready?"
}

text HF_T_Mike6_Yes{
    "Haha! I knew you would be excited,\n"
    "{PLAYER}!\l"
    "Follow me!"
}

text HF_T_Mike6_No{
    "Oh, come on, don't be like that,\n"
    "{PLAYER}.\l"
    "Just follow me, would'ya?"
}

text HF_T_Mike7{
    "Well, what do you think?!\p"
    "It's our very own battle stage!!\p"
    "I wanted to make sure that our first\n"
    "battle was in an arena just like Sammy\l"
    "and Agatha used to compete in!\p"
    "Well... {PAUSE 30}Pretty close, right?\p"
    "Let's go, {PLAYER}, we gotta test it out\n"
    "right away!"
}

text HF_T_Mike8{
    "Hurry up, {PLAYER}!\n"
    "Go stand on your side so we can get\l"
    "this battle started!!"
}

text HF_T_Mike9{
    "Don't be timid now, {PLAYER}!\n"
    "Get back here and battle!"
}

text HF_T_Mike10{
    "Let's do this!"
}

text HF_T_Mike11{
    "Man, I was really hoping I could win\n"
    "that first one...\p"
    "But no matter what, that was the most\n"
    "fun I've ever had!\p"
    "It seems like we both have a good bond\n"
    "with our Pokemon too.\p"
    "Anyways, I gotta go n' let this lil' fella\n"
    "rest for a while.\p"
    "See ya later, {PLAYER}!"
}

text HF_T_Mike12{
    "Hey, {PLAYER}!\p"
    "Just wanted to stop by before I hit the\n"
    "road!\p"
    "I'm gonna head North on Rt. 89 toward\n"
    "Verde Village, Bo said there was some\l"
    "sort of ''Trainer School'' there.\p"
    "Not that I need anyone to teach me how\n"
    "to battle or anything, haha!\p"
    "Anyways, if you're heading the same way\n"
    "and run into that pesky flock of\l"
    "Psyduck, just turn your Pok√©mon loose\l"
    "on 'em!\p"
    "Don't let anyone beat you before we\n"
    "battle again, {PLAYER}.\p"
    "Your first loss is going to be to me!\n"
    "See ya!"

}

text HF_T_MikeGoTurtwig{
    "Go, Turtwig!"
}

text HF_T_MikeGoLitten{
    "Go, Litten!"
}

text HF_T_MikeGoOshawott{
    "Go, Oshawott!"
}

text HF_T_MikeLoseBattle{
    "Not the start I was lookin' for..."
}

text HF_T_Mamaw1{
    "Takin' care of Millie brings ol' Mamaw so\n"
    "much joy.\p"
    "I jus' love seein' her happy!"
}

text HF_T_Mom1{
    "Honey, where are you going?!\n"
    "You're taking this too personally!\p"
    "...{PAUSE 30}\n"
    "{FONT_SMALL_NARROWER}Oh boy."
}